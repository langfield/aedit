// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ STATEMENT ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// A ``diff`` is a sequence of 0 or more (possibly malformed) statements.
diff: stmt*

// A ``stmt`` is either an ``insert``, a ``delete``, or an ``update``.

stmt: insert | update | delete | nt_delete | bad
insert: "INSERT" "INTO" insertion ";"
update: "UPDATE" TABLE "SET" assignments "WHERE" row ";"
delete: "DELETE" "FROM" TABLE "WHERE" row ";"

command: "INSERT" "INTO" | "DELETE" "FROM" | "UPDATE"
bad: command UNUSED /[^\n;]+/ ";"

row: "id=" INT | "ntid=" INT "AND" "ord=" INT

assignments: assignment ("," assignment)*
assignment: COLUMN "=" value
TABLE: /notes/ | /col/ | /cards/ | /decks/ | /fields/ | /notetypes/ | /templates/
UNUSED: /graves/ | /sqlite_stat[0-9]+/ | /config/

insertion: note | card | deck | field | notetype | template
note: NOTES_SCHEMA "VALUES(" nid "," guid "," mid "," mod "," usn "," tags "," flds "," sfld "," csum "," flags "," data ")"
card: CARDS_SCHEMA "VALUES(" cid "," nid "," did "," ord "," mod "," usn "," type "," queue "," due "," ivl "," factor "," reps "," lapses "," left "," odue "," odid "," flags "," data ")"
deck: DECKS_SCHEMA "VALUES(" did "," deckname "," mtime_secs "," usn "," common "," kind ")"
field: FIELDS_SCHEMA "VALUES(" ntid "," ord "," fieldname "," config ")"
notetype: NOTETYPES_SCHEMA "VALUES(" ntid "," ntname "," mtime_secs "," usn "," config ")"
template: TEMPLATES_SCHEMA "VALUES(" ntid "," ord "," tmplname "," mtime_secs "," usn "," config ")"

NOTES_SCHEMA: /notes/ "(id,guid,mid,mod,usn,tags,flds,sfld,csum,flags,data)"
CARDS_SCHEMA: /cards/ "(id,nid,did,ord,mod,usn,type,queue,due,ivl,factor,reps,lapses,\"left\",odue,odid,flags,data)"
DECKS_SCHEMA: /decks/ "(id,name,mtime_secs,usn,common,kind)"
FIELDS_SCHEMA: /fields/ "(ntid,ord,name,config)"
NOTETYPES_SCHEMA: /notetypes/ "(id,name,mtime_secs,usn,config)"
TEMPLATES_SCHEMA: /templates/ "(" ("id" | "ntid") ",ord,name,mtime_secs,usn,config)"

COLUMN: NOTES_COLUMN | CARDS_COLUMN | COLLECTION_COLUMN | DECKS_COLUMN

NOTES_COLUMN: "id" | "guid" | "mid" | "mod" | "usn" | "tags" | "flds" | "sfld" | "csum" | "flags" | "data"
CARDS_COLUMN: "nid" | "did" | "ord" | "type" | "queue" | "due" | "ivl" | "factor" | "reps" | "lapses" | "\"left\"" | "odue" | "odid"
COLLECTION_COLUMN: "crt" | "scm" | "ver" | "ls" | "conf" | "models" | "decks" | "dconf"
DECKS_COLUMN: "name" | "mtime_secs" | "common" | "kind"

value: INT | SIGNED_INT | bytestring
sfld: NUMBER | bytestring

flds: bytestring
deckname: bytestring
fieldname: bytestring
ntname: bytestring
tmplname: bytestring
bytestring: seq WS? ("||" seq WS?)*

seq: bytes | STRING
bytes: "X'" (HEXDIGIT HEXDIGIT)+ "'"

// Cards
cid: INT
nid: INT
did: INT
ord: INT
mod: INT
usn: SIGNED_INT
type: INT
queue: SIGNED_INT
due: SIGNED_INT
ivl: SIGNED_INT
factor: INT
reps: INT
lapses: INT
left: INT
odue: INT
odid: INT
flags: INT
data: STRING

// Notes
guid: STRING
mid: INT
tags: STRING
csum: INT

// Decks
mtime_secs: INT
common: BLOB
kind: BLOB

// Fields
ntid: INT
config: BLOB


// SQLite3 blob literals.
BLOB: "x'" HEXDIGIT* "'"

// Single-quoted strings (single quotes must be escaped, which in SQL means they are doubled-up).
STRING: /'[^']*(?:''[^']*)*'/

// === IMPORTS ===

%import common.SIGNED_INT
%import common.HEXDIGIT
%import common.NEWLINE
%import common.NUMBER
%import common.INT
%import common.WS
%ignore WS
