// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ STATEMENT ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// A ``diff`` is a sequence of 0 or more (possibly malformed) statements.
diff: stmt*

// A ``stmt`` is either an ``insert``, a ``delete``, or an ``update``.

stmt: insert | update | delete | bad
insert: "INSERT" "INTO" insertion ";"
update: "UPDATE" TABLE "SET" assignments "WHERE" row ";"
delete: "DELETE" "FROM" TABLE "WHERE" row ";"

command: "INSERT" "INTO" | "DELETE" "FROM" | "UPDATE"
bad: command UNUSED /[^\n;]+/ ";"

row: "id=" INT
assignments: assignment ("," assignment)*
assignment: COLUMN "=" value
TABLE: /notes/ | /col/ | /cards/
UNUSED: /graves/ | /sqlite_stat[0-9]+/

insertion: note | card | deck
note: NOTES_SCHEMA "VALUES(" nid "," guid "," mid "," mod "," usn "," tags "," flds "," sfld "," csum "," flags "," data ")"
card: CARDS_SCHEMA "VALUES(" cid "," nid "," did "," ord "," mod "," usn "," type "," queue "," due "," ivl "," factor "," reps "," lapses "," left "," odue "," odid "," flags "," data ")"
deck: DECKS_SCHEMA "VALUES(" did "," deckname "," mtime_secs "," usn "," common "," kind ")"

NOTES_SCHEMA: /notes/ "(id,guid,mid,mod,usn,tags,flds,sfld,csum,flags,data)"
CARDS_SCHEMA: /cards/ "(id,nid,did,ord,mod,usn,type,queue,due,ivl,factor,reps,lapses,\"left\",odue,odid,flags,data)"
DECKS_SCHEMA: /decks/ "(id,name,mtime_secs,usn,common,kind)"

COLUMN: NOTES_COLUMN | COLLECTION_COLUMN

NOTES_COLUMN: "id" | "guid" | "mid" | "mod" | "usn" | "tags" | "flds" | "sfld" | "csum" | "flags" | "data"
COLLECTION_COLUMN: "crt" | "mod" | "scm" | "ver" | "usn" | "ls" | "conf" | "models" | "decks" | "dconf" | "tags"

value: INT | SIGNED_INT | bytestring
sfld: NUMBER | bytestring

flds: bytestring
deckname: bytestring
bytestring: seq WS? ("||" seq WS?)*

seq: bytes | STRING
bytes: "X'" (HEXDIGIT HEXDIGIT)+ "'"

// Cards
cid: INT
nid: INT
did: INT
ord: INT
mod: INT
usn: SIGNED_INT
type: INT
queue: SIGNED_INT
due: SIGNED_INT
ivl: SIGNED_INT
factor: INT
reps: INT
lapses: INT
left: INT
odue: INT
odid: INT
flags: INT
data: STRING

// Notes
guid: STRING
mid: INT
tags: STRING
csum: INT

// Decks
name: STRING
mtime_secs: INT
common: BLOB
kind: BLOB

// SQLite3 blob literals.
BLOB: "x'" HEXDIGIT* "'"

// Single-quoted strings (single quotes must be escaped, which in SQL means they are doubled-up).
STRING: /'[^']*(?:''[^']*)*'/

// === IMPORTS ===

%import common.SIGNED_INT
%import common.HEXDIGIT
%import common.NEWLINE
%import common.NUMBER
%import common.INT
%import common.WS
%ignore WS
