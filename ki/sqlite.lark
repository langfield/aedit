// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ STATEMENT ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// A ``diff`` is a sequence of 0 or more (possibly malformed) statements.
diff: stmt*

// A ``stmt`` is either an ``insert``, a ``delete``, or an ``update``.

stmt: insert | update | delete
insert: "INSERT" "INTO" TABLE COLUMN_SET values ";"
update: "UPDATE" TABLE "SET" assignments "WHERE" row ";"
delete: "DELETE" "FROM" TABLE "WHERE" row ";"
bad: /.+/

row: "id=" INT
assignments: assignment ("," assignment)*
assignment: COLUMN "=" value
TABLE: NOTES
values: "VALUES(" INT "," STRING "," INT "," INT "," SIGNED_INT "," STRING "," fields "," sfld "," INT "," INT "," STRING ")"

COLUMN: "id" | "guid" | "mid" | "mod" | "usn" | "tags" | "flds" | "sfld" | "csum" | "flags" | "data"

value: INT | SIGNED_INT | bytestring
sfld: SIGNED_INT | bytestring

NOTES: "notes"
fields: bytestring
bytestring: seq WS? ("||" seq WS?)*
COLUMN_SET: "(id,guid,mid,mod,usn,tags,flds,sfld,csum,flags,data)"

seq: bytes | STRING
bytes: "X'" (HEXDIGIT HEXDIGIT)+ "'"

// Single-quoted strings (single quotes must be escaped, which in SQL means they are doubled-up).
STRING: /'[^']*(?:''[^']*)*'/

// === IMPORTS ===

%import common.SIGNED_INT
%import common.HEXDIGIT
%import common.NEWLINE
%import common.INT
%import common.WS
%ignore WS
