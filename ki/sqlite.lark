// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ STATEMENT ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// A ``diff`` is a sequence of 0 or more (possibly malformed) statements.
diff: stmt*

// A ``stmt`` is either an ``insert``, a ``delete``, or an ``update``.

stmt: insert | update | delete
insert: "INSERT" "INTO" TABLE COLUMN_SET values ";"
update: "UPDATE" TABLE "SET" assignments "WHERE" row ";"
delete: "DELETE" "FROM" TABLE "WHERE" row ";"
bad: /.+/

row: "id=" INT
assignments: assignment ("," assignment)*
assignment: COLUMN "=" value
TABLE: NOTES
values: "VALUES(" value ("," value)* ")"

COLUMN: "id" | "guid" | "mid" | "mod" | "usn" | "tags" | "flds" | "sfld" | "csum" | "flags" | "data"

value: INT | SIGNED_INT | note_fields | SINGLE_QUOTED_STRING_NOT_FIELD

NOTES: "notes"
note_fields: field_sequence WS? ("||" field_sequence WS?)*
COLUMN_SET: "(id,guid,mid,mod,usn,tags,flds,sfld,csum,flags,data)"

field_sequence: byte_sequence | string_sequence
string_sequence: SINGLE_QUOTED_STRING
byte_sequence: "X'" (HEXDIGIT HEXDIGIT)+ "'"

// Single-quoted strings (single quotes must be escaped, which in SQL means they are doubled-up).
SINGLE_QUOTED_STRING: /'[^']*(?:''[^']*)*'/
SINGLE_QUOTED_STRING_NOT_FIELD: /'[^']*(?:''[^']*)*'(?!\|)/

// === IMPORTS ===

%import common.SIGNED_INT
%import common.HEXDIGIT
%import common.NEWLINE
%import common.INT
%import common.WS
%ignore WS
