// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ NOTE ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// A ``note`` is a group of exactly 1 ``header`` and at least 1 ``field``.
note: header "\n" tags "\n" field* lastfield

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ FIELD ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// Only allow one newline after a field, but it can have many newlines within
// it, and fields can be empty.
field: fieldheader FIELDLINE*
lastfield: fieldheader FIELDLINE*
fieldheader: "##" " "* ANKINAME
FIELDLINE: "\n" FIELDCHAR*
FIELDCHAR: /(?!##)(?:[^\0\x07\x08\x0b\x0c\r\n])/

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ HEADER ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// A ``header`` is sequence of metadata rules, all required.
header: (title TRIPLEBACKTICKS GUID NOTETYPE TRIPLEBACKTICKS)


// === TITLE ===

// Titles are alphanumeric + some limited punctuation, and must start with a
// nonspace char. Exactly 1 space between #s and start of title.
title: "#" TITLENAME "\n"+
TITLENAME: TITLECHAR (" " | TITLECHAR)*
TITLECHAR: /[a-zA-Z0-9_-]/


// === GUID ====

// GUID field is allowed to be empty.
GUID: "guid:" " "* (GUIDCHAR*)? "\n"


// = NOTETYPE ==

NOTETYPE: "notetype:" " "* ANKINAME "\n"


// === TAGS ====

// A ``tags`` object is allowed to be empty.
// We do NOT check that tags do not contain whitespace, because this is
// difficult.
tags: ("### Tags\n" TRIPLEBACKTICKS [TAGNAME ("\n" TAGNAME)* "\n"] TRIPLEBACKTICKS)
TAGNAME: /[^\s\0\x07\x08\x0b\x0c`"]/+

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ COMMON ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// === CHARS ===

// Anki strips leading and trailing spaces from field names (you can test this
// in the desktop client).
ANKINAME: STARTENDCHAR ANKICHAR* (/[\t ]/+ ANKICHAR+)*
ANKICHAR: /[^\s:{}"\0\x07\x08]/
GUIDCHAR: /[^\s"\0\x07\x08]/
STARTENDCHAR: /[^#^\/\s:{}"\0\x07\x08]/
TRIPLEBACKTICKS: "```\n"


// === IMPORTS ===

%import common.ESCAPED_STRING
%import common.INT
%import common.WS_INLINE
%import common.LF
%ignore WS_INLINE
